#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   Sorin Ionescu <sormin.ionescu@gmail.com>
#

# Load dependencies.
pmodload 'helper'

function prompt_sormin_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_sormin_pwd="$MATCH"
    unset MATCH
  else
    _prompt_sormin_pwd="%b%F{white}${pwd:t}%f"
  fi
}

function prompt_sormin_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_sormin_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_sormin_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_sormin_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %F{red}%%%f'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %F{green}❮%F{yellow}❮%F{red}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%F{green}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%F{yellow}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%F{yellow}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format ':%F{green}%b%f'
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format ' %%F{red}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%F{blue}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format ' %%F{magenta}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%F{cyan}★%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%F{yellow}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%F{white}◎%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '%F{bright_magenta}%f$(coalesce "%b" "%p" "%c")%s' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'

  # Define prompts.
  PROMPT='%b%F{white}${_prompt_sormin_pwd}%f${git_info:+${(e)git_info[prompt]}}%(!. %F{red}#%f%b.)${editor_info[keymap]} '
  RPROMPT='${editor_info[overwrite]}%(?:: %F{red}⏎%f)${VIM:+" %F{green}V%f%b"}${git_info[rprompt]}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_sormin_setup "$@"

