#
# Defines environment variables.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Phillip Ridlen <p@rdln.net>
#

#
# Browser
#

if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

#
# Editors
#

export EDITOR='vim'
export VISUAL='vim'
export PAGER='less'

#
# Language
#

if [[ -z "$LANG" ]]; then
  eval "$(locale)"
fi

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-F -g -i -M -R -S -w -X -z-4'

# Set the Less input preprocessor.
if (( $+commands[lesspipe.sh] )); then
  export LESSOPEN='| /usr/bin/env lesspipe.sh %s 2>&-'
fi

#
# Paths
#

typeset -gU cdpath fpath mailpath path
typeset -gxU MANPATH
typeset -gxUT INFOPATH infopath

# Set the the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that info searches for manuals.
infopath=(
  /usr/local/share/info
  /usr/share/info
  $infopath
)

# Set the list of directories that man searches for manuals.
manpath=(
  /usr/local/share/man
  /usr/share/man
  $manpath
)

for path_file in /etc/manpaths.d/*(.N); do
  manpath+=($(<$path_file))
done
unset path_file

export GOPATH=$HOME/.go
export GOBIN=$GOPATH/bin
export CARGOPATH=$HOME/.cargo
export CARGOBIN=$CARGOPATH/bin
# Set the list of directories that Zsh searches for programs.
path=()
path=(
  ./bin
  ./.bin
  ./vendor/bundle/bin
  $HOME/.rbenv/bin
  $HOME/bin
  $HOME/.bin
  $GOBIN
  $CARGOBIN
  /usr/local/{bin,sbin}
  /usr/local/share/npm/bin
  /usr/{bin,sbin}
  /{bin,sbin}
  $path
)

for path_file in /etc/paths.d/*(.N); do
  path+=($(<$path_file))
done
unset path_file

#
# Function Paths
#
export ZSH_HOME="$DOTS/zsh"
fpath=(
  $ZSH_HOME/themes
  $ZSH_HOME/functions
  $fpath
)

#
# Temporary Files
#

if [[ -d "$TMPDIR" ]]; then
  export TMPPREFIX="${TMPDIR%/}/zsh"
  if [[ ! -d "$TMPPREFIX" ]]; then
    mkdir -p "$TMPPREFIX"
  fi
fi

#
# Load Python virtualenv
#
# export WORKON_HOME=~/src/python
# source /usr/local/bin/virtualenvwrapper.sh

#
# NVM - Node Version Manager
#
export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && . "$NVM_DIR/nvm.sh"  # This loads nvm

#
# Privates
#

if [[ -f "$HOME/.zprivate" ]] ; then
  source "$HOME/.zprivate"
fi


